prototype(Unikka.Slick:Content.Slide) < prototype(Neos.Fusion:Component) {
    backgroundImage = Neos.Neos:ImageUri {
        asset = ${ q(node).property('backgroundImage') }
    }

    classNames = Neos.Fusion:RawArray {
        class = 'slide__inner'
        lozad = 'lozad'
        lozad.@if.enableLazy = ${ q(node).property('lazyLoading') }
        backgroundImage = 'slide__inner__backgroudImage'
        backgroundImage.@if.hasBackground = ${ q(node).property('backgroundImage') }
        additionalClass = ${ q(node).property('additionalClass') }
        @process.collectionClass >
    }

    renderer = Neos.Fusion:Case {
        noBackground {
            condition = ${ !props.backgroundImage && node.context.inBackend }
            renderer = afx`<Unikka.Slick:Components.Notification classNames={props.classNames} key='notification.noBackground' default='Please add an background image in the inspector' />`
        }
        default {
            condition = true
            renderer = afx`<Unikka.Slick:Components.SlideContent classNames={props.classNames} />`
        }
    }
}
